\section{The HeadMeetBeaconEvent Test Class}

The HeadMeetBeaconEvent class implements a test event where the head
of a train meets a beacon. It provides a SetBeacon and an Execute
operation. The execute operation returns the current announcements and
speed restrictions.

\begin{vdm_al}
class HeadMeetBeaconEvent is subclass of Event

instance variables

beacon : Beacon;

operations

public HeadMeetBeaconEvent: Beacon ==> HeadMeetBeaconEvent
HeadMeetBeaconEvent(b) ==
  beacon := b;

public Execute: CSL ==> Test`TestResult
Execute(csl) ==
( csl.HeadMeetsBeacon(beacon); 
  let anns = csl.GetAnnouncements(),
      restr = csl.GetSpeedRestrictions()
  in
    return mk_Test`BeaconsMet(
            [ mk_Test`Announce(anns(i).GetTargetSpeed())  | 
              i in set inds anns ],
            [ mk_Test`Limit(restr(i).GetSpeedRestriction()) |
              i in set inds restr ]) );

end HeadMeetBeaconEvent
\end{vdm_al}
